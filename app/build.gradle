import org.apache.commons.io.FilenameUtils

apply plugin: 'com.android.application'

versioning {
    dirty = { version -> "$version"}
    displayMode = 'base'
}

group = "fr.coppernic.samples"
version = versioning.info.display

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "fr.coppernic.sample.serial"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode versioning.info.versionCode
        versionName versioning.info.display
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            String buildType = variant.variantData.variantConfiguration.buildType.name
            String ext = FilenameUtils.getExtension("$output.outputFile")

            // Handle release
            if (buildType == "release") {
                buildType = ""
            } else {
                buildType = "-${buildType}"
            }

            String newName = "Serial${buildType}-${version}.${ext}"
            logger.debug "${project.name} output : $newName"
            output.outputFile = new File(output.outputFile.parent, newName)
        }
    }
}

File keystorePropertiesFile = rootProject.file("../../../Android/config/android/signing/standard/keystore.properties")
File store = rootProject.file("../../../Android/config/android/signing/standard/standard.jks")

if (keystorePropertiesFile.exists() && store.exists()) {
// Initialize a new Properties() object called keystoreProperties.
    Properties keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    android {
        signingConfigs {
            standard {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile store
                storePassword keystoreProperties['storePassword']
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.standard
            }
        }
    }
} else {
    println "Files not found : $keystorePropertiesFile, $store"
}

String support = "25.3.1"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile "com.android.support:appcompat-v7:$support"
    compile "com.android.support:design:$support"
    compile "com.android.support:support-vector-drawable:$support"
    compile "com.android.support:support-v4:$support"
    compile "com.android.support:preference-v7:$support"
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    compile(group: 'fr.coppernic.sdk.core', name: 'CpcCore', version: '1.0.1', ext: 'aar') {
        transitive = true
    }
    compile(group: 'fr.coppernic.sdk.cpcutils', name: 'CpcUtilsLib', version: '6.9.0', ext: 'aar')
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile('com.afollestad.material-dialogs:core:0.9.4.2') {
        exclude group: 'com.android.support'
    }
    testCompile 'junit:junit:4.12'
}
